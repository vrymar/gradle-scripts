buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.researchgate:gradle-release:3.0.2"
    }
}

apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: net.researchgate.release.ReleasePlugin

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    getArchiveClassifier().set('sources')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    getArchiveClassifier().set('javadoc')
}

javadoc {
    failOnError = false
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    url "https://github.com/vrymar/${project.name}"
    packaging 'jar'
    licenses {
        license {
            name "MIT License"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            name 'Vitalii Rymar'
            email 'vitalii.stas.rymar@gmail.com'
        }
    }
    scm {
        connection "scm:git:https://github.com/vrymar/${project.name}.git"
        developerConnection "scm:git:ssh://git@github.com:vrymar/${project.name}.git"
        url "https://github.com/vrymar/${project.name}/tree/master"
    }
}

release {
    pushReleaseVersionBranch = 'main'
}


project.ext.githubUserName = project.hasProperty('githubUserName') ? githubUserName : ""
project.ext.githubToken = project.hasProperty('githubToken') ? githubToken : ""
project.ext.publishRepo = project.hasProperty('publishRepo') ? publishRepo : ""

publishing {
    repositories {
        maven {
            name = "Github"
            url = uri(publishRepo)
            credentials {
                username = githubUserName
                password = githubToken
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId 'org.vrymar'
            artifactId "$project.name"

            pom {
                name = "${project.name}"
                description = "${project.description}"
            }

            pom.withXml {
                def root = asNode()
                root.children().last() + pomConfig
            }
            artifact sourcesJar
            artifact javadocJar
        }
    }

}

afterReleaseBuild.dependsOn publish
